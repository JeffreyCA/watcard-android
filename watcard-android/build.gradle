def ver = '1.8'

group = 'ca.jeffrey.watcard'
version = ver

apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 15
        versionName ver
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'watcard-android'
            description 'An Android-compatible Java library for retrieving UWaterloo WatCard data'
            url 'https://github.com/JeffreyCA/watcard-android'
            inceptionYear '2016'

            packaging 'aar'
            groupId 'ca.jeffrey.watcard'
            artifactId 'watcard-android'
            version ver

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/JeffreyCA/watcard-android.git'
                url 'https://github.com/JeffreyCA/watcard-android'
            }
            developers {
                developer {
                    name 'Jeffrey'
                    id "jeffreyca"
                    email "jeffreyca16@gmail.com"
                }
            }
        }
    }
}

Properties properties = new Properties()
File propFile = "${System.properties['user.home']}${File.separator}.gradle${File.separator}bintray.properties" as File
properties.load(propFile.newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = 'watcard-android'
		userOrg = 'jeffreyca'
        websiteUrl = 'https://github.com/JeffreyCA/watcard-android'
        vcsUrl = 'https://github.com/JeffreyCA/watcard-android.git'
        licenses = ['Apache-2.0']
        publish = true

        version {
            name = ver
            desc = 'Stable Release'
            vcsTag = ver
        }
    }
}

dependencies {
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.4'
    compile 'org.jsoup:jsoup:1.10.1'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    baseName = 'watcard-android'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    baseName = 'watcard-android'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
