def ver = '1.0'

group = 'ca.jeffrey.watcard'
version = ver

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "jeffreyca"
            name "Jeffrey"
            email "jeffreyca16@gmail.com"
        }
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'ca.jeffrey.watcard'
            artifactId 'watcard-android'
            version ver
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An Android-compatible Java library for retrieving UWaterloo WatCard data')
                root.appendNode('name', 'watcard-android')
                root.appendNode('url', 'https://github.com/JeffreyCA/watcard-android')
                root.children().last() + pomConfig
            }
        }
    }
}

Properties properties = new Properties()
File propFile = "${System.properties['user.home']}${File.separator}.gradle${File.separator}bintray.properties" as File
properties.load(propFile.newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['MyPublication']

    pkg {
        repo = 'maven'
        name = 'watcard-android'
		userOrg = 'jeffreyca'
        websiteUrl = 'https://github.com/JeffreyCA/watcard-android'
        vcsUrl = 'https://github.com/JeffreyCA/watcard-android.git'
        licenses = ['Apache-2.0']
        publish = true

        version {
            name = ver
            desc = 'Stable Release'
            vcsTag = ver
        }
    }
}

dependencies {
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.4'
    compile 'org.jsoup:jsoup:1.10.1'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
